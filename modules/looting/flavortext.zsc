extend class UaS_Looting_Handler {
	array<string> ItemStrings;
	array<string> JoinerWords;
	array<string> Keywords;
	array<string> Conditions;

	string FlavorText(bool foundSomething) {
		if (ItemStrings.Size() == 0 || JoinerWords.Size() == 0) { InitializeStrings(); }
		string returnMessage = "";
		if (UaS.RandomChance(0.5)) { returnMessage = returnMessage.."There's"; }
		returnMessage = returnMessage.." nothing";
		if (foundSomething) { returnMessage = returnMessage.." else"; }
		if (UaS.RandomChance(0.5)) { returnMessage = returnMessage.." here"; }
		if (UaS.RandomChance(0.5)) {
			returnMessage = returnMessage..".";
			return returnMessage;
		}
		returnMessage = returnMessage..JoinerWords[random(0, JoinerWords.Size() - 1)];
		returnMessage = returnMessage..BuildItemString();
		return returnMessage;
	}

	string BuildItemString() {
		string item = ItemStrings[random(0, ItemStrings.Size() - 1)];

		// Collapse substrings
		while (true) {
			int LeftBrace, RightBrace;
			LeftBrace = item.RightIndexOf("{");
			RightBrace = item.IndexOf("}",LeftBrace+1);
			if (LeftBrace == -1 || RightBrace == -1) { break; }

			string substring = item.Mid(LeftBrace+1, (RightBrace-LeftBrace)-1);
			item.Remove(LeftBrace+1, (RightBrace-LeftBrace)-1);

			array<string> substrings;
			substring.Split(substrings,"|");

			item.Replace("{}", substrings[random(0,substrings.Size()-1)]);
		}

		// Remove double-spaces
		while (true) {
			if (item.IndexOf("  ", 0) > -1) { item.Replace("  ", " "); }
			else { break; }
		}

		console.printf(item);
		return item;
	}

	void InitializeStrings() {
		// Define substring lists with { } (curly brackets)
		// Lists may be nested!
		// Separate string items with | (pipe)
		// Add leading or trailing pipe characters for chance of blank strings
		ItemStrings.Push(" {an empty|{a {half-eaten|spoiled|rotten|moldy|||}}} {ration|ration wrapper}{, covered in filth|}");
		ItemStrings.Push(" a {small|} collection of {bent|rusty|} grenade pins.");
		ItemStrings.Push(" {a revenant|an imp} finger bone.");
		ItemStrings.Push(" a {hairy||||} wad of chewing gum.");
		ItemStrings.Push(" {an oily|{a rusty|a damaged|a}} gun part.");
		ItemStrings.Push(" a {small|large|||} {rusty|shiny|chipped|||} {gold|silver|copper||||||} coin.");
		ItemStrings.Push(" some {tarnished|empty|dented|cracked} {brass|steel|polymer} casings.");
		ItemStrings.Push(" {some|a piece of} pocket lint.");
		ItemStrings.Push(" {an empty|a {cracked|broken|}} potion bottle{, missing a cork|. The cork is tightly sealed|.}");
		ItemStrings.Push(" a {broken|} PDA. {It looks like it's been {crushed with a hammer.|shot.}}");
		ItemStrings.Push(" a pinup poster{|||||||||| of Betty Grable| of Imp-tan}.");
		ItemStrings.Push(" two floppy disks, labeled \"DOOM.\"");
		ItemStrings.Push(" 27 floppy disks, labeled \"Windows 95.\"");
		ItemStrings.Push(" a {torn|dirty|scorched} paper titled \"TPS REPORT.\"");
		ItemStrings.Push(" a movie script. {There are notes written in the margins.}");
		ItemStrings.Push(" some {electrical|USB} cables.");
		ItemStrings.Push(" an empty {beer|soda|wine|champagne} bottle. {It is {cracked|broken|unlabeled}.}");
		ItemStrings.Push(" a family photo. {One of the faces has been {scratched|burned|scribbled|cut} out.}");
		ItemStrings.Push(" {an action figure|a doll}. {|||||||||||||It is naked.}");
		ItemStrings.Push(" a {VHS tape|DVD} titled \"{Demolition Man|The Matrix|Total Recall|The Way of the Gun|Falling Down}.\"");
		ItemStrings.Push(" the secret Ugly as Sin feature.");
		ItemStrings.Push(" a glass jar.");
		ItemStrings.Push(" {an SD|a micro-SD|a {PS1|PS2|flash} memory} card.");
		ItemStrings.Push(" some old paper currency. {It's worthless.|}");
		ItemStrings.Push(" a {small|large|||} plush {emoji toy|cacodemon|teddy bear|furbie}.");
		ItemStrings.Push(" a {Rainbow Dash|Twilight Sparkle|Pinkie Pie|Fluttershy|Applejack|Rarity} figurine.");
		ItemStrings.Push(" a {delicate|intricate|chunky||||} {gold|silver} chain. {There is a charm on it.||||}");
		ItemStrings.Push(" two A{A|AA} batteries.");
		ItemStrings.Push(" a {Walkman-brand} {tape|CD} player.");
		ItemStrings.Push(" a cassete tape{.| labeled \"DOOM.\"}");

		JoinerWords.Push(", except");
		JoinerWords.Push(", except for");
		JoinerWords.Push(", aside from");
		JoinerWords.Push(", apart from");
		JoinerWords.Push(", besides");
		JoinerWords.Push(", beyond");
	}
}
