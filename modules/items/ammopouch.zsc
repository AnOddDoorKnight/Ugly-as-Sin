//==================================================
// Base Definition
//==================================================
class UaS_AmmoPouch : HDBackpack {
	default {
		tag "Ammo Pouch";
		HDBackpack.MaxCapacity 200;
		Inventory.Icon "UGSIJ0";
		Inventory.PickupMessage "Picked up an ammo pouch!";
		scale 0.6;
		hdweapon.refid UAS_HDLD_AMMOPOUCH;
	}

	override string, double GetPickupSprite() { return "UGSIJ0", 1.0; }

	override void BeginPlay() {
		super.BeginPlay();
		Storage = new('AmmoPouch_ItemStorage');
		UpdateCapacity();
	}

	States {
		Spawn:
			UGSI J -1 NoDelay;
			Stop;
	}
}

//==================================================
// Bulk and Multiple Pouches
//==================================================
extend class UaS_AmmoPouch {
	override double WeaponBulk() {
		double reductionfactor = 0.45 + min(0.05 * amount, 0.5);
		double basebulk = 20 * amount;
		//console.printf("ammopouch: bulk factor "..reductionfactor.." --- base bulk: "..basebulk);
		return max((Storage ? Storage.TotalBulk * reductionfactor : 0), basebulk);
	}

	override void UpdateCapacity() {
		MaxCapacity = default.MaxCapacity / max(hd_encumbrance, 0.01);
		Storage.MaxBulk = MaxCapacity * amount;
	}

	override void actualpickup(actor other,bool silent) {
		UaS_AmmoPouch HeldPouch=UaS_AmmoPouch(other.findinventory("UaS_AmmoPouch"));
		if (HeldPouch) {
			HeldPouch.amount++;
			HeldPouch.UpdateCapacity();
			for (int i = 0; i < Storage.Items.Size(); i++) {
				StorageItem MoveItem = Storage.Items[i];
				int AmountToMove = MoveItem.Amounts.Size() > 0 ? MoveItem.Amounts[0] : 0;
				//console.printf(""..AmountToMove);
				HeldPouch.Storage.AddAmount(MoveItem.ItemClass, AmountToMove);
				Storage.RemoveItem(MoveItem, null, null, AmountToMove);
			}
			self.destroy();
			return;
		}
		super.actualpickup(other, silent);
	}

	override inventory CreateTossable(int amount) {
		UaS_AmmoPouch DroppedPouch = UaS_AmmoPouch(super.CreateTossable(amount));
		StorageItem SelItem = Storage.GetSelectedItem();
		if (!SelItem) { return DroppedPouch; }
		int AmountInPouch = SelItem.Amounts.Size() > 0 ? SelItem.Amounts[0] : 0;
		double UnitBulk = HDPickup(getdefaultbytype(SelItem.ItemClass)).bulk * hdmath.getencumbrancemult();
		int AmountToMove = int((DroppedPouch.Storage.MaxBulk - DroppedPouch.Storage.TotalBulk) / UnitBulk);
		AmountToMove = min(AmountToMove, AmountInPouch);
		//console.printf(""..SelItem.GetBulk());
		//console.printf(""..UnitBulk);
		//console.printf(""..AmountToMove);
		//console.printf("AmountInPouch "..AmountInPouch);
		DroppedPouch.Storage.AddAmount(SelItem.ItemClass, AmountToMove);
		Storage.RemoveItem(SelItem, null, null, AmountToMove);
		return DroppedPouch;
	}
}

//==================================================
// Item Storage Definitions
// Expand this later with UASINFO?
//==================================================
class AmmoPouch_ItemStorage : ItemStorage {
	override int CheckConditions(Inventory item, class<Inventory> cls) {
		bool valid = (
			(item && (item is 'HDRoundAmmo' || item is 'FourMilAmmo')) ||
			(cls  && (cls  is 'HDRoundAmmo' || item is 'FourMilAmmo'))
		);

		if (!valid) { return IType_Invalid; }
		return super.CheckConditions(item,cls);
	}

	override int GetOperationSpeed(class<Inventory> item, int operation) {
		switch (clamp(operation, 0, 2)) {
			case 0: return 3; break;	//extract
			case 1: return 1; break;	//pocket
			case 2: return 1; break;	//insert
		}
		return 10;
	}
}

//============================================
// HUD stuff, mostly copypasta from HDBackpack
//============================================
extend class UaS_AmmoPouch {
	override void DrawHUDStuff(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl)
	{
		int BaseOffset = -80;

		sb.DrawString(sb.pSmallFont, "\c[DarkBrown][] [] [] \c[Tan]Ammo Pouch\c[DarkBrown] [] [] []", (0, BaseOffset), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER);
		string BulkString = "Total Bulk: \cf"..int(Storage.TotalBulk).."\c-";
		if  (amount > 1) { BulkString = BulkString.." --- Pouches: \cf"..amount.."\c-"; }
		sb.DrawString(sb.pSmallFont, BulkString, (0, BaseOffset + 10), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER);

		int ItemCount = Storage.Items.Size();

		if (ItemCount == 0)
		{
			sb.DrawString(sb.pSmallFont, "No items found.", (0, BaseOffset + 30), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER, Font.CR_DARKGRAY);
			return;
		}

		StorageItem SelItem = Storage.GetSelectedItem();
		if (!SelItem)
		{
			return;
		}

		for (int i = 0; i < (ItemCount > 1 ? 5 : 1); ++i)
		{
			int RealIndex = (Storage.SelItemIndex + (i - 2)) % ItemCount;
			if (RealIndex < 0)
			{
				RealIndex = ItemCount - abs(RealIndex);
			}

			vector2 Offset = ItemCount > 1 ? (-100, 8) : (0, 0);
			switch (i)
			{
				case 1: Offset = (-50, 4);  break;
				case 2: Offset = (0, 0); break;
				case 3: Offset = (50, 4); break;
				case 4: Offset = (100, 8); break;
			}

			StorageItem CurItem = Storage.Items[RealIndex];
			bool CenterItem = Offset ~== (0, 0);
			sb.DrawImage(CurItem.Icons[0], (Offset.x, BaseOffset + 40 + Offset.y), sb.DI_SCREEN_CENTER | sb.DI_ITEM_CENTER, CenterItem && !CurItem.HaveNone() ? 1.0 : 0.6, CenterItem ? (50, 30) : (30, 20), CenterItem ? (4.0, 4.0) : (3.0, 3.0));
		}

		sb.DrawString(sb.pSmallFont, SelItem.NiceName, (0, BaseOffset + 60), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER, Font.CR_FIRE);

		int AmountInBackpack = SelItem.ItemClass is 'HDMagAmmo' ? SelItem.Amounts.Size() : (SelItem.Amounts.Size() > 0 ? SelItem.Amounts[0] : 0);
		sb.DrawString(sb.pSmallFont, "In pouch:  "..sb.FormatNumber(AmountInBackpack, 1, 6), (0, BaseOffset + 70), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER, AmountInBackpack > 0 ? Font.CR_BROWN : Font.CR_DARKBROWN);

		int AmountOnPerson = GetAmountOnPerson(hpl.FindInventory(SelItem.ItemClass));
		sb.DrawString(sb.pSmallFont, "On person:  "..sb.FormatNumber(AmountOnPerson, 1, 6), (0, BaseOffset + 78), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_CENTER, AmountOnPerson > 0 ?  Font.CR_WHITE : Font.CR_DARKGRAY);

	}
}
