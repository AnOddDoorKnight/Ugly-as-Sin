class UaS_BleedingControl : UaS_TraumaKit {
	int pressure, hemostatic;

	Default {
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "";
		-INVENTORY.INVBAR;
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.IGNORESKILL;
		+WEAPON.NO_AUTO_SWITCH;
		//+WEAPON.NOAUTOFIRE;
		weapon.slotpriority 0.4;
		weapon.slotnumber 9;
		tag "Bleeding Control";
	}
	override double weaponbulk(){ return 0; }

	override void DoEffect() {
		if (!(owner.player.readyweapon is 'UaS_BleedingControl')) { return; }

		statusMessage = "--- \cjBleeding Control\c- ---\n\n\n";
		statusMessage = statusmessage..
			"\cuApply pressure and\nhemostatic compound\nto bandaged wounds.\n\n";

		BleedingControl();
		TickMessages();
		A_WeaponMessage(statusMessage);
	}

	void BleedingControl() {
		HDPlayerPawn plr = HDPlayerPawn(owner);
		if (plr.unstablewoundcount <= 0) {
			currentmessage.text = "";
			statusMessage = statusmessage.."No unstable bandaged wounds to treat.";
			return;
		}

		// Hemostatic amount readout
		if (hemostatic > 0) { statusMessage = statusmessage.."\cr"; }
		for (int i = 0; i < hemostatic; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."Hemostatic";
		for (int i = 0; i < hemostatic; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."\n";

		// Pressure amount readout
		if (pressure > 50) { statusMessage = statusmessage.."\cw"; }
		if (pressure > 100) { statusMessage = statusmessage.."\cm"; }
		for (int i = 0; i < pressure/10; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."Pressure";
		for (int i = 0; i < pressure/10; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."\n\n";

		// Apply hemostatic compound
		if ((owner.player.cmd.buttons & BT_RELOAD) && !(owner.player.oldbuttons & BT_RELOAD)) {
			if (hemostatic > 1) {
				currentmessage.text = "Already used hemostatic compound.";
				currentmessage.timeout = 35;
				return;
			}
			else {
				hemostatic = 5;
				currentmessage.text = "Applied hemostatic compound to wound.";
				currentmessage.timeout = 35;
				return;
			}
		}

		// Apply pressure
		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			pressure = min(pressure + 1, 125);
		}
		else {
			pressure = max(pressure - 1, 0);
		}

		if (level.time % 10 == 0 && pressure <= 100) {
			int upperlimit = pressure + (hemostatic * 5);
			int check = (
				random[uas_bc](0, upperlimit) +
				random[uas_bc](0, upperlimit) +
				random[uas_bc](0, upperlimit)) / 3;

			if (check > 75) {
				currentmessage.text = "Stabilized some bleeding.";
				currentmessage.timeout = 35;
				hemostatic = max(hemostatic - 1, 0);
				pressure = 0;
				plr.unstablewoundcount = max(plr.unstablewoundcount - 1, 0);
				plr.oldwoundcount = max(plr.oldwoundcount + 1, 0);
			}
		}
	}
}
