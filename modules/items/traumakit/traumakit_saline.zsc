extend class UaS_TraumaKit {
	void HandleSaline() {
		statusMessage = statusmessage.."\cjSaline\n";
		statusMessage = statusmessage.."\cuNeutral wound debriding solution.\n\n";
		if (!currentWound) { return; }

		// Display contextual status
		if (currentWound.dirty > 0) {
			currentmessage.text = "The wound is dirty.\n";
			currentmessage.timeout = 1*35;
		}
		else {
			currentmessage.text = "The wound appears clean.\n";
			currentmessage.timeout = 1*35;
			return;
		}

		// Handle actions
		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			if (!(owner.player.oldbuttons & BT_ATTACK)) {
				weaponstatus[TK_HOLD] = random[uas_tk](20,35);
				owner.A_StartSound("misc/smallslop", CHAN_WEAPON);
			}
			else {
				weaponstatus[TK_HOLD]--;
			}

			if (weaponstatus[TK_HOLD] > 0) {
				currentmessage.text = "Flushing the wound...\n";
				currentmessage.timeout = 10;
				currentWound.dirty = max(currentWound.dirty - random[uas_tk](0,1), 0);
				currentWound.infection = max(currentWound.infection - randompick[uas_tk](0,0,0,0,1), 1);
			}

		}
	}
}
