class HDScav_Scarcity_Spawner : EventHandler {
	double ScarcityFactor;

	override void WorldThingSpawned(WorldEvent e) {
		bool replaced = false;
		ScarcityFactor = double(HDScav_ScarcityRate) / 100.0;

		// Conditions for skipping spawn adjustment
		if(!HDScav.RandomChance(ScarcityFactor)) { return; }
		if(level.time > 1) { return; }
		if(!e.Thing) { return; }
		if(e.Thing.bDROPPED) { return; }

		// Randomly break/reduce packed ammo pickups
		switch(e.Thing.GetClassName()) {
			case 'HD9mMag15':
				replaced = MagReplacer('HD9mMag15', 'HDLoose9mm', 15, e.Thing);
				break;
			case 'HD9mMag30':
				replaced = MagReplacer('HD9mMag30', 'HDLoose9mm', 30, e.Thing);
				break;
			case 'HD9mBoxPickup':
				hdupk(e.Thing).amount = BoxReducer('HD9mBoxPickup', 'HDLoose9mm', 100, e.Thing);
				break;
			case 'HD4mMag':
				replaced = MagReplacer('HD4mMag', 'FourMilAmmo', 50, e.Thing);
				break;
			case 'HD7mMag':
				replaced = MagReplacer('HD7mMag', 'SevenMilAmmo', 30, e.Thing);
				break;
			case 'HD7mBoxPickup':
				hdupk(e.Thing).amount = BoxReducer('HD7mBoxPickup', 'SevenMilAmmo', 50, e.Thing);
				break;
			case 'HD7mClip':
				replaced = MagReplacer('HD7mClip', 'SevenMilAmmo', 10, e.Thing);
				break;
			case 'ShellPickup':
				HDScav.SpawnStuff('HDShellAmmo', random(0, 4 * min(1.25-ScarcityFactor, 1.0)), e.Thing.pos, true);
				replaced = true;
				break;
			case 'ShellBoxPickup':
				hdupk(e.Thing).amount = BoxReducer('ShellBoxPickup', 'HDShellAmmo', 20, e.Thing);
				break;
			case 'HDBattery':
				int MagCap = 20 * min(1.25 - ScarcityFactor, 1.0);
				HDMagAmmo.SpawnMag(e.Thing, 'HDBattery', random(0, MagCap));
				replaced = true;
				break;
			case 'RocketBigPickup':
				for(int i = 1; i <= 8; i++) {
					if(HDScav.RandomChance(0.5)) { HDScav.SpawnStuff('HDRocketAmmo', random(0, 1), e.Thing.pos); }
					else { HDScav.SpawnStuff('DudRocket', random(0, 1), e.Thing.pos); }
				}
				HDScav.SpawnStuff('HDScav_EmptyRocketBox', 1, e.Thing.pos);
				replaced = true;
				break;
		}

		// Randomly break/reduce medical and health items
		switch(e.Thing.GetClassName()) {
			case 'PortableMedikit':
				if(HDScav.RandomChance(0.5)) { HDScav.SpawnStuff('PortableStimpack', 1, e.Thing.pos); }
				let spawneditem = HDMedikitter(actor.Spawn('HDMedikitter', e.Thing.pos));
				if(!spawneditem) { break; }
				spawneditem.injectableflesh = random(0, 2);
				spawneditem.usedpatches = random(0, HD_MEDPATCHLIMIT * ScarcityFactor);
				if(spawneditem.injectableflesh < 2 || spawneditem.usedpatches > 0) { spawneditem.frame = 2; }
				HDScav.SpawnStuff('HDScav_EmptyMedikit', 1, e.Thing.pos);
				replaced = true;
				break;
			case 'PortableStimpack':
				if(HDScav.RandomChance(0.5)) { HDScav.SpawnStuff('SpentStim', random(0, 1), e.Thing.pos); }
				else { HDScav.SpawnStuff('HDScav_StimInjector', random(0, 1), e.Thing.pos); }
				HDScav.SpawnStuff('HDScav_EmptyStimpack', 1, e.Thing.pos);
				replaced = true;
				break;
			case 'PortableBerserkPack':
				for(int i = 1; i <= 4; i++) {
					if(HDScav.RandomChance(0.5)) { HDScav.SpawnStuff('SpentZerk', random(0, 1), e.Thing.pos); }
					else { HDScav.SpawnStuff('HDScav_ZerkInjector', random(0, 1), e.Thing.pos); }
				}
				HDScav.SpawnStuff('HDScav_EmptyZerkpack', 1, e.Thing.pos);
				replaced = true;
				break;
			case 'BluePotion':
				if(HDScav.RandomChance(0.5)) {
					HDScav.SpawnStuff('SpentBottle', 1, e.Thing.pos);
					HDScav.SpawnStuff('SpentCork', 1, e.Thing.pos);
					replaced = true;
				}
				break;
		}

		// Delete the original object if it was replaced
		if(replaced == true) {
			if(HDScav_Debug >=1) { console.printf("%s replaced", e.Thing.GetClassName()); }
			e.Thing.Destroy();
		}

	}

	//Generic function for breaking/replacing map-spawned magazines
	bool MagReplacer(name MagName, name ammoName, int MagCap, actor spawnPointer) {
		bool returnValue;
		int rounds, packed;
		MagCap *= min(1.25 - ScarcityFactor, 1.0); //Possible spawn at least *some* ammo even at max scarcity
		rounds = random(1, MagCap); //Pick a number up to cap
		//Pack some in a mag, or no mag at all
		if(HDScav.RandomChance(0.5)) {
			packed = randompick(0, random(1, rounds)); //50%: pack none (spawn empty), or up to all
			rounds -= packed;
			HDMagAmmo.SpawnMag(spawnPointer, MagName, packed);
		}
		//spill the rest on the ground
		HDScav.SpawnStuff(ammoName, rounds, spawnPointer.pos);
		returnValue = true;
		return returnValue;
	}

	//Generic function for reducing amount in map-spawned ammo boxes, without removal since boxes are already rare
	int BoxReducer(name BoxName, name ammoName, int BoxCap, actor spawnPointer) {
		int rounds, loose;
		BoxCap *= min(1.25 - ScarcityFactor, 1.0); //Possible spawn at least *some* ammo even at max scarcity
		rounds = random(BoxCap * 0.5, BoxCap); //Pick a number from 50% up to cap
		//Drop random amount of on ground
		if(HDScav.RandomChance(0.5)) {
			loose = random(1, rounds * 0.5); //spill up to 50%
			rounds -= loose;
			HDScav.SpawnStuff(ammoName, loose, spawnPointer.pos);
		}
		//Pack the rest into the box.
		return rounds;
	}
}