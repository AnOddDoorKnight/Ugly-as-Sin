const UAS_HDLD_STABILZER = "stb";
const UAS_STB_SLOT = 25;

class UaS_GyroStabilizer : HDPickup {
	array<string> weaponList;
	array<int> AttachList;
	string statusText;
	double stabilize;

	Default {
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "Picked up a gyro stabilizer.";
		Inventory.Icon "HSCVI0";
		HDPickup.Bulk 10;
		Scale 0.75;
		tag "Gyroscopic Stabilizer";
		hdpickup.refid UAS_HDLD_STABILZER;
	}

	override void DoEffect() {
		super.DoEffect();
		DoStabilize();
	}

	void DoStabilize() {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return; }
		if(!ownr.player.ReadyWeapon) { return; } // Skip if no ready weapon
		HDWeapon wep = HDWeapon(owner.player.ReadyWeapon); // Cast the readyweapon
		if (!(wep is "HDFist") && CheckAttached(wep) >= 0) {
			stabilize += GetCorrections();
			stabilize += ownr.vel.length();
			stabilize *= 0.9;
			double maxfactor = max(stabilize/(stabilize+20),0.25);
			console.printf(""..maxfactor);
			MultiplyRecoil(maxfactor);
			if (maxfactor >= 0.75) { statusText = "\crCALIBRATING \cu"..stabilize; }
			else if (maxfactor >= 0.5) { statusText = "\cqSTABILIZING \cu"..stabilize; }
			else { statusText = "\cdSTABILIZED \cu"..stabilize; }
		}
	}

	bool Periodic(int p) { return (level.maptime % p == 0); }

	int GetCorrections() {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return 0; }
		int cvalue = (
			ownr.muzzleclimb1.length()  +
			ownr.muzzleclimb2.length()  +
			ownr.muzzleclimb3.length()  +
			ownr.muzzleclimb4.length()  +
			ownr.hudbobrecoil1.length() +
			ownr.hudbobrecoil2.length() +
			ownr.hudbobrecoil3.length() +
			ownr.hudbobrecoil4.length() +
			ownr.muzzledrift.length());
		return cvalue;
		// return ceil(cvalue / max(cvalue-1,1));
	}

	void MultiplyRecoil(double factor) {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return; }
		ownr.muzzleclimb1  *= factor;
		ownr.muzzleclimb2  *= factor;
		ownr.muzzleclimb3  *= factor;
		ownr.muzzleclimb4  *= factor;
		ownr.hudbobrecoil1 *= factor;
		ownr.hudbobrecoil2 *= factor;
		ownr.hudbobrecoil3 *= factor;
		ownr.hudbobrecoil4 *= factor;
		ownr.muzzledrift   *= factor;
		if (ownr.scopecamera) {
			ownr.scopecamera.angle *= factor;
			ownr.scopecamera.pitch *= factor;
		}
	}

	override void DrawHudStuff(hdstatusbar sb, hdplayerpawn hpl, int hdflags, int gzflags) {
		sb.drawstring(sb.pnewsmallfont,
			statusText,
			(50,-50),
			flags:sb.DI_TEXT_ALIGN_LEFT|sb.DI_SCREEN_HCENTER|sb.DI_SCREEN_BOTTOM,
			translation:Font.CR_GRAY,scale:(0.5,0.5)
		);
	}

	override bool Use(bool pickup) {
		if(!owner.player.ReadyWeapon) { return false; } // Skip if no ready weapon
		HDWeapon wep = HDWeapon(owner.player.ReadyWeapon); // Cast the readyweapon
		AttachOrDetatch(wep);
		return false;
	}

	void AttachOrDetatch(HDWeapon atdt) {
		int index = CheckAttached(atdt);
		if (index == -1) { AttachToWeapon(atdt); }
		else { DetachFromWeapon(atdt, index); }
	}

	// -1 means not in attach list
	int CheckAttached(HDWeapon cw) {
		int ret = -1;
		for (int i = 0; i < AttachList.size(); i++) {
			if (cw.weaponstatus[UAS_STB_SLOT] == AttachList[i]) {
				ret = i;
				break;
			}
		}
		return ret;
	}

	void DetachFromWeapon(HDWeapon dt, int index = -1) {
		if (index >= 0) {
			dt.weaponstatus[UAS_STB_SLOT] = 0;
			AttachList.delete(index);
			console.printf("Detatched index "..index);
			A_StartSound("misc/fragknock", CHAN_BODY, volume: 0.3);
			statusText = "";
		}
	}

	void AttachToWeapon(HDWeapon at) {
		int UID = random(int.max/4, int.max);
		at.weaponstatus[UAS_STB_SLOT] = UID;
		AttachList.push(UID);
		console.printf("Attached with UID "..UID);
		A_StartSound("misc/fragknock", CHAN_BODY, volume: 0.3);
		A_StartSound("misc/casing", CHAN_WEAPON, volume: 0.3);
	}

	States {
		Spawn:
			HSCV I -1;
			stop;
	}
}
