version "3.4"

class AlreadyInitialized : Inventory {}

class HDScav_Bootstrap : EventHandler {
	override void PlayerEntered(PlayerEvent e) {
		PlayerInfo player = players[e.PlayerNumber];
		if(!player.mo.countinv("AlreadyInitialized")) {
			player.mo.giveinventory("HDScav_HungerTracker", 1);
			player.mo.giveinventory("HDScav_FoodItem", 3);
			player.mo.giveinventory("AlreadyInitialized", 1);
		}
	}

	override void WorldThingSpawned(WorldEvent e) {
		if(e.Thing.GetClassName() == "HDBP") {
			for(int i=1; i <= 3; i++) {
				let spawneditem = HDScav_FoodItem(Actor.Spawn("HDScav_FoodItem", e.Thing.pos, false));
				spawneditem.vel.x += frandom(-1,1);
				spawneditem.vel.y += frandom(-1,1);
				if(random(i,4) == 4) { break; }
			}
		}
	}
}

class HDScav_HungerTracker : CustomInventory {
	int calories;
	int minfatigue;
	HDPlayerPawn ownr;

	default {
		+INVENTORY.AUTOACTIVATE;
		+INVENTORY.PERSISTENTPOWER;
	}

	override void PostBeginPlay() {
		super.PostBeginPlay();
		calories = random(1500,2000);
	}

	override void DoEffect() {
		super.DoEffect();
		let ownr = HDPlayerPawn(owner);

		//Per minute
		if(level.time % 2100 == 1) {
			if(calories == 0) { minfatigue += 2; }
			else { minfatigue -= 2; }
			if(minfatigue < 0) { minfatigue = 0; }
			if(hd_debug) { A_Log(string.format("Energy: %i", calories)); }
			if(hd_debug) { A_Log(string.format("MinFatigue: %i", minfatigue)); }
		}

		//Per second
		if(level.time % 35 == 1) {
			calories -= 1;
			calories -= (ownr.fatigue - minfatigue);
			if(calories < 0) { calories = 0; }
		}

		//Per tic
		if(ownr.fatigue < minfatigue) { ownr.fatigue = minfatigue; }
	}

	States {
		Spawn:
			TNT1 A -1;
			stop;
		Use:
			TNT1 A 1;
			loop;
	}
}

class HDScav_FoodItem : HDPickup {
	HDScav_FoodEater eater;

	default {
		inventory.pickupmessage "Picked up some food.";
		inventory.icon "HSCVA0";
		inventory.MaxAmount 8;
		hdpickup.bulk 10;
		scale 0.5;
	}

	action void HDScav_OpenFood() {
		if(!countinv("HDScav_FoodEater")) { GiveInventory("HDScav_FoodEater",1); }
		invoker.eater = HDScav_FoodEater(FindInventory("HDScav_FoodEater", false));
		if(invoker.eater.mealSize <= 0) {
			invoker.eater.mealSize = random(10,15)*50;
			TakeInventory("HDScav_FoodItem",1);
		}
		A_SelectWeapon("HDScav_FoodEater");
	}

	States {
		Spawn:
			HSCV A -1;
			stop;
		Use:
			TNT1 A 1 HDScav_OpenFood();
			loop;
	}
}

class HDScav_EmptyFoodItem : HDDebris {
	default {
		scale 0.4;
		bouncesound "misc/wallchunks";
		bouncefactor 0.5;
	}

	States {
		Spawn:
			HSCV C 1;
			Wait;
		Death:
			HSCV C -1;
			stop;
	}
}

class HDScav_FoodEater : HDWoundFixer {
	HDScav_HungerTracker tracker;
	int biteSize;
	int mealSize;
	string flavor;

	action string RandomFlavor() {
		switch(random(0,5))	{
			case 0:
				return "'Chili w/Beans'\nThe cheese will back you up for a month...";
				break;
			case 1:
				return "'Shredded Barbeque Beef'\nAnd 'Carb Fortified Beverage Powder'\nto wash it down.";
				break;
			case 2:
				return "'Meatballs in Marinara Sauce'\nIts-a me, Doomguy!";
				break;
			case 3:
				return "'Chili and Macaroni'\nOh boy, that means Skittles for desert!";
				break;
			case 4:
				return "'Spinach Mushrooms & Cream Sauce Fettuccine'\nMmm, eatin' fancy in the warzone tonight.";
				break;
			case 5:
				return "' Rib Shaped BBQ Pork Patty'\nJust like mamma used to make back home...";
				break;
		}
		return "'Generic Military Ration'";
	}

	action state HDScav_CheckFood() {
		if(invoker.mealSize <= 0) {
			A_WeaponMessage("No more food!",175);
			let empty=spawn("HDScav_EmptyFoodItem",pos+(0,0,height-8));
			empty.angle=angle+2;
			empty.vel=vel;
			empty.A_ChangeVelocity(3,1,1,CVF_RELATIVE);
			A_SelectWeapon("Ring");
			return ResolveState("nope");
		}
		return ResolveState(null);
	}

	action void HDScav_TakeScoop() {
		A_PlaySound("HDScav/FoodScoop", CHAN_AUTO, frandom(0.3, 0.6));
		invoker.biteSize  = min(invoker.mealsize, 75) * frandom(0.75,1.0);
		invoker.biteSize *= frandom(0.75,1.0);
		invoker.biteSize  = clamp(invoker.biteSize, 1, 75);
		A_WeaponMessage(string.format("%s\n\nFood left: %i", invoker.flavor, invoker.mealSize),175);
	}

	action state HDScav_Consume() {
		invoker.tracker = HDScav_HungerTracker(FindInventory("HDScav_HungerTracker", false));
		if(invoker.bitesize > 0) {
			//Chew
			int chunk = random(5,10);
			A_PlaySound("weapons/pocket", CHAN_AUTO, frandom(0.25, 0.4));
			A_PlaySound("misc/fragroll", CHAN_AUTO, frandom(0.25, 0.5));
			invoker.mealSize -= clamp(chunk, 0, invoker.biteSize);
			invoker.tracker.calories += clamp(chunk, 0, invoker.biteSize);
			invoker.biteSize -= clamp(chunk, 0, invoker.biteSize);
			//console.printf("%i",countinv("HDScav_CalorieItem"));
			return ResolveState(null);
		}
		A_WeaponMessage(string.format("%s\n\nFood left: %i", invoker.flavor, invoker.mealSize),175);
		return ResolveState("Swallow");
	}

	States {
		Spawn:
			TNT1 A 1;
			Stop;
		Select:
			TNT1 A 10 {
				if(!getcvar("hd_helptext")) return;
				invoker.flavor = RandomFlavor();
				A_WeaponMessage(string.format("%s",invoker.flavor).."\n\nHold Fire to eat.\nRemember to chew your food!",250);

			}
			goto Super::Select;
		DeselectHold:
			TNT1 A 1;
			TNT1 A 0 A_Refire("DeselectHold");
			TNT1 A 0{
				A_SelectWeapon("Ring");
				A_WeaponReady(WRF_NOFIRE);
			}
			goto nope;
		Fire:
		Scoop:
			TNT1 A random(15,20) HDScav_TakeScoop();
			TNT1 A 0 A_Refire("Consume");
			goto nope;
		Consume:
			TNT1 A random(8,12) HDScav_Consume();
			TNT1 A 0 A_Refire("Consume");
		Swallow:
			TNT1 A random(5,10) A_PlaySound("weapons/pocket");
			TNT1 A 0 HDScav_CheckFood();
			goto Ready;
	}
}