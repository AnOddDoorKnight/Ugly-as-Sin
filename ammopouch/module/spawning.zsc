//============================================
// Spawning, 25% chance on corpses / boxes
//============================================
class UaS_WildAmmoPouch : IdleDummy {
	override void postbeginplay() {
		super.postbeginplay();
		let SpawnedPouch=UaS_AmmoPouch(spawn("UaS_AmmoPouch",pos,ALLOW_REPLACE));
		SpawnedPouch.vel = vel;
		SpawnedPouch.RandomContents();
		self.destroy();
	}
}

class UaS_AmmoPouchSpawner : EventHandler {
	override void WorldThingSpawned(WorldEvent e) {
		if(level.maptime > 1) { return; }
		if(!e.Thing) { return; }
		if(e.Thing is "Inventory" && Inventory(e.Thing).Owner) { return; }

		bool spawnable = (
			e.Thing.GetClassName() == "HDAmBox" ||
			e.Thing.GetClassName() == "DeadRifleman" ||
			e.Thing.GetClassName() == "ReallyDeadRifleman");

		if(spawnable && randompick(0,0,0,1) == 1) {
			let SpawnedPouch = Actor.Spawn('UaS_WildAmmoPouch', (e.Thing.pos.x, e.Thing.pos.y, e.Thing.pos.z + 5));
			SpawnedPouch.vel.x += frandom(-2,2);
			SpawnedPouch.vel.y += frandom[spawnstuff](-2,2);
			SpawnedPouch.vel.z += frandom[spawnstuff](1,2);
			if (e.Thing.GetClassName() == "HDAmBox") { e.Thing.destroy(); }
		}
	}
}
